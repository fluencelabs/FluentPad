service Peer("peer"):
	is_connected: string -> bool

service Op("op"):
	identity: -> ()

data ServiceInstance:
	peer_id: string
	service_id: string

data App:
  history: ServiceInstance
  user_list: ServiceInstance

data EmptyServiceResult:
	ret_code: s32
	err_msg: string

data User:
	peer_id: string
	relay_id: string
	name: string

data GetUsersServiceResult:
	users: []User
	ret_code: s32
	err_msg: string

data AuthResult:
	ret_code: s32
	err_msg: string
	is_authenticated: bool

service UserList:
	is_authenticated: -> AuthResult
	get_users: -> GetUsersServiceResult
	join: User -> EmptyServiceResult
	leave: string -> EmptyServiceResult
	is_exists: string -> ()

data AddServiceResult:
	ret_code: s32
	err_msg: string
	entry_id: u64

data HistoryEntry:
	id: u64
	body: string

data GetEntriesServiceResult:
	ret_code: s32
	err_msg: string
	entries: []HistoryEntry

service History:
	get_all: bool -> GetEntriesServiceResult
	get_last: u64, bool -> GetEntriesServiceResult
	add: string, bool -> AddServiceResult
	set_tetraplet: string, string, string, string -> EmptyServiceResult

service FluentPad("fluence/fluent-pad"):
	notifyOnline: string, bool -> () -- user peerId, onlineStatus
	notifyUserAdded: User, bool -> () -- user peerId, onlineStatus
	notifyUserRemoved: string -> () -- user peerId,
	notifyTextUpdate: string, string, bool -> () -- authorPeerId, changes, isAuthorized

func join(app: App, user: User) -> EmptyServiceResult:
	on app.user_list.peer_id:
		UserList app.user_list.service_id
		res <- UserList.join(user)
	<- res

func getUserList(app: App) -> []User:
	on app.user_list.peer_id:
		UserList app.user_list.service_id
		allUsers <- UserList.get_users()
	<- allUsers.users

func notifySelfAdded(app: App, me: User):
	allUsers <- getUserList(app)
	for user <- allUsers:
		on user.relay_id:
			Op.identity()
		par on user.peer_id:
			FluentPad.notifyUserAdded(me, true)

func updateOnlineStatuses(app: App, updateStatus: bool -> (), callback: string, bool -> ()):
	allUsers <- getUserList(app)
	for user <- allUsers:
		on user.relay_id:
			Op.identity()
		par on user.peer_id:
			isOnline <- Peer.is_connected(user.peer_id)
			callback(user.peer_id, isOnline)

func leave(app: App, currentUserName: string):
	on app.user_list.peer_id:
		UserList app.user_list.service_id
		res <- UserList.leave(currentUserName)
	allUsers <- getUserList(app)
	for user <- allUsers:
		on user.relay_id:
			Op.identity()
		par on user.peer_id:
			FluentPad.notifyUserRemoved(currentUserName)

func auth(app: App) -> AuthResult:
	on app.user_list.peer_id:
		UserList app.user_list.service_id
		res <- UserList.is_authenticated()
	<- res

func getHistory(app: App) -> GetEntriesServiceResult:
	authRes <- auth(app)
	on app.history.peer_id:
		History app.history.service_id
		res <- History.get_all(authRes.is_authenticated)
	<- res
